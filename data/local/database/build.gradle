import com.enricog.tempo.buildsrc.Libs
import com.enricog.tempo.buildsrc.Versions

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'com.enricog.data.local.database'

    compileSdkVersion Versions.compileSdk

    defaultConfig {
        minSdkVersion Versions.minSdk

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                compilerArgumentProviders(
                        new RoomSchemaArgProvider(new File(projectDir, "schemas"))
                )
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    File schemaDir

    RoomSchemaArgProvider(File schemaDir) {
        this.schemaDir = schemaDir
    }

    @Override
    Iterable<String> asArguments() {
        return ["-room.schemaLocation=${schemaDir.path}".toString()]
    }
}

dependencies {
    implementation project(':data:routines:api')
    implementation project(':data:timer:api')

    implementation Libs.Kotlin.coroutines
    implementation Libs.Kotlin.serialization

    // Room
    implementation Libs.Room.runtime
    ksp Libs.Room.compiler
    implementation Libs.Room.ktx

    // Data Store
    implementation Libs.DataStore.preferences

    // Hilt
    implementation Libs.Hilt.android
    kapt Libs.Hilt.androidCompiler

    testImplementation project(':data:routines:testing')
    testImplementation project(':data:timer:testing')
    testImplementation project(":core:coroutines:testing")
    testImplementation Libs.AndroidX.Test.core
    testImplementation Libs.AndroidX.Test.rules
    testImplementation Libs.AndroidX.Test.junitKtx
    testImplementation Libs.Test.junit
    testImplementation Libs.Test.truth

    androidTestImplementation project(':data:routines:testing')
    androidTestImplementation project(':data:timer:testing')
    androidTestImplementation project(":core:coroutines:testing")
    androidTestImplementation Libs.Room.testing
    androidTestImplementation Libs.AndroidX.Test.junitKtx
    androidTestImplementation Libs.Test.junit
    androidTestImplementation Libs.Test.truth
    androidTestImplementation Libs.AndroidX.Test.core
    androidTestImplementation Libs.AndroidX.Test.rules
}