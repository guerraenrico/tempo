import com.enricog.tempo.buildsrc.Libs
import com.enricog.tempo.buildsrc.Versions

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'

    id 'com.github.triplet.play'
}

play {
    serviceAccountCredentials.set(file(getPropValue("SERVICE_ACCOUNT_FILE_PATH", "")))
    releaseName.set("${Versions.appVersionCode} (${Versions.appVersionName})")
    track.set("internal")
}

android {
    namespace 'com.enricog.tempo'

    compileSdkVersion Versions.compileSdk

    defaultConfig {
        applicationId 'com.enricog.tempo'
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.appVersionCode
        versionName Versions.appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(getPropValue("KEY_STORE_FILE_PATH", ""))
            storePassword getPropValue("KEY_STORE_PASSWORD", "")
            keyAlias getPropValue("KEY_ALIAS", "")
            keyPassword getPropValue("KEY_ALIAS_PASSWORD", "")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.compilerVersion
    }
}

dependencies {
    implementation project(":base:extensions")
    implementation project(":core:coroutines:api")
    implementation project(":core:logger:api")
    implementation project(':data:routines:api')
    implementation project(':data:local:database')
    implementation project(":features:timer")
    implementation project(":features:routines")
    implementation project(':ui:components')
    implementation project(":ui:theme")
    implementation project(":navigation:api")
    implementation project(":libraries:sound:api")

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.appCompat
    implementation Libs.Google.material

    // Room
    implementation Libs.Room.runtime
    kapt Libs.Room.compiler
    implementation Libs.Room.ktx

    // Compose
    implementation platform(Libs.AndroidX.Compose.bom)
    androidTestImplementation platform(Libs.AndroidX.Compose.bom)
    implementation Libs.AndroidX.Compose.ui
    implementation Libs.AndroidX.Compose.material
    implementation Libs.AndroidX.Compose.tooling
    implementation Libs.AndroidX.Compose.navigation
    implementation Libs.AndroidX.Compose.activity
    implementation Libs.Google.Accompanist.navigation

    // Hilt
    implementation Libs.Hilt.android
    kapt Libs.Hilt.androidCompiler
    implementation Libs.Hilt.navigation

    // Firebase
    implementation platform(Libs.Firebase.bom)
    implementation Libs.Firebase.crashlytics
    implementation Libs.Firebase.analytics

    coreLibraryDesugaring Libs.Tools.desugar
}