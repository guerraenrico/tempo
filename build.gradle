import com.enricog.tempo.buildsrc.Libs
import com.enricog.tempo.buildsrc.Versions

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0-beta05'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Libs.Kotlin.version}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:${Libs.Hilt.androidVersion}"

        classpath 'com.google.gms:google-services:4.3.5'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.0'

        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.4.1"

        classpath 'com.github.triplet.gradle:play-publisher:3.2.0-agp4.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.osacky.doctor" version "0.7.0"
}
apply plugin: "org.jlleitschuh.gradle.ktlint"

allprojects {
    repositories {
        google()
        mavenCentral()
        // TODO: remove when this are migrated to mavenCentral
        jcenter() {
            content {
                includeModule("org.jetbrains.kotlinx", "kotlinx-collections-immutable-jvm")
                includeModule("com.linkedin.dexmaker", "dexmaker")
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
    }
}

subprojects {
    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def group = details.requested.group
            def module = details.requested.module.name

            if (group == 'org.jetbrains.kotlin' && module == 'kotlin-stdlib-jdk7') {
                // Force kotlin-stdlib module version
                details.useTarget Libs.Kotlin.stdlib
            }

            if (group == 'androidx.activity' && module == 'activity-compose') {
                // FIXME Force activity-compose version; next compose release should fix this
                details.useTarget Libs.AndroidX.Compose.activity
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs += [
                    // Enable experimental coroutines APIs, including Flow
                    "-Xopt-in=kotlin.RequiresOptIn",
                    "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                    "-Xuse-experimental=kotlin.contracts.ExperimentalContracts",
                    "-Xuse-experimental=kotlin.time.ExperimentalTime",
                    "-Xuse-experimental=kotlin.ExperimentalStdlibApi",
                    "-Xuse-experimental=androidx.compose.material.ExperimentalMaterialApi",

                    "-Xskip-prerelease-check",

                    // "-P",
                    // "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true"
            ]

            jvmTarget = Versions.jvmTarget
        }
    }

    apply plugin: "org.jlleitschuh.gradle.ktlint"
    ktlint {
        version = "0.40.0"
        debug = true
        verbose = true
        android = true
        outputToConsole = true
        outputColorName = "RED"
        ignoreFailures = true
        disabledRules = [
                "max-line-length"
        ]
        reporters {
            reporter "plain"
            reporter "checkstyle"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}