import com.enricog.tempo.buildsrc.Libs
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0-beta03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Libs.Kotlin.version}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:${Libs.Hilt.androidVersion}"

        classpath 'com.google.gms:google-services:4.3.4'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.osacky.doctor" version "0.7.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def group = details.requested.group
            def module = details.requested.module.name

            if (group == 'org.jetbrains.kotlin' && module == 'kotlin-stdlib-jre7') {
                // Force kotlin-stdlib module instead since kotlin-stdlib-jre7 no longer exists
                details.useTarget Libs.Kotlin.stdlib
            }
        }
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions {

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
            freeCompilerArgs += "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-Xuse-experimental=kotlin.contracts.ExperimentalContracts"
            freeCompilerArgs += "-Xuse-experimental=kotlin.time.ExperimentalTime"
            freeCompilerArgs += "-Xuse-experimental=kotlin.ExperimentalStdlibApi"
            freeCompilerArgs += "-Xuse-experimental=androidx.compose.material.ExperimentalMaterialApi"

            freeCompilerArgs += ["-Xallow-jvm-ir-dependencies", "-Xskip-prerelease-check"]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}