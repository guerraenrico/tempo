import com.enricog.tempo.buildsrc.Libs
import com.enricog.tempo.buildsrc.Versions

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Libs.Kotlin.version}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:${Libs.Hilt.androidVersion}"

        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'

        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.1.0"

        classpath 'com.github.triplet.gradle:play-publisher:3.6.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.osacky.doctor" version "0.7.0"
}
apply plugin: "org.jlleitschuh.gradle.ktlint"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
            freeCompilerArgs += "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-Xuse-experimental=kotlin.contracts.ExperimentalContracts"
            freeCompilerArgs += "-Xuse-experimental=kotlin.time.ExperimentalTime"
            freeCompilerArgs += "-Xuse-experimental=kotlin.ExperimentalStdlibApi"
            freeCompilerArgs += "-Xuse-experimental=androidx.compose.material.ExperimentalMaterialApi"
            freeCompilerArgs += "-Xuse-experimental=androidx.compose.foundation.ExperimentalFoundationApi"
            freeCompilerArgs += "-Xuse-experimental=androidx.compose.ui.ExperimentalComposeUiApi"
            freeCompilerArgs += "-opt-in=androidx.compose.animation.ExperimentalAnimationApi"
            freeCompilerArgs += "-opt-in=kotlinx.coroutines.FlowPreview"
            freeCompilerArgs += "-Xskip-prerelease-check"

            // "-P",
            // "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true"

            jvmTarget = Versions.jvmTarget
        }
    }

    apply plugin: "org.jlleitschuh.gradle.ktlint"
    ktlint {
        version = "0.41.0"
        debug = true
        verbose = true
        android = true
        outputToConsole = true
        outputColorName = "RED"
        ignoreFailures = true
        disabledRules = [
                "max-line-length"
        ]
        reporters {
            reporter "plain"
            reporter "checkstyle"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

Object getPropValue(varName, defaultValue) {
    def value = project.properties[varName]
    if (value == null) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('config.properties').newDataInputStream())
        value = properties.getProperty(varName) ?: defaultValue
    }
    return value
}
